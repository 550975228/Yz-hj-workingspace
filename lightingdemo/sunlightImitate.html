<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <title>测试用例</title>
    <style>
        body {
            margin: 0;
            height: 100%;
            width: 100%;
            color: red;
        }

        canvas {
            display: block;
        }
    </style>
    <script id="shadowVertexShader" type="x-shader/x-vertex">
        //设置阴影顶点着色器
        attribute vec4 a_Position;
        attribute vec4 a_Color;
        uniform mat4 u_MvpMatrix;
        varying vec4 v_Color;
        void main() {
            gl_Position = u_MvpMatrix * a_Position;
            v_Color = a_Color;
        }
    </script>
    <script id="shadowFragmentShader" type="x-shader/x-vertex">
        //设置阴影片元着色器
        precision highp float;
        varying vec4 v_Color;
        const vec4 bitShift = vec4(1.0, 256.0, 256.0 * 256.0, 256.0 * 256.0 * 256.0);
        const vec4 bitMask = vec4(1.0/256.0, 1.0/256.0, 1.0/256.0, 0.0);
        vec4 packDepth (float depth) {
            vec4 rgbaDepth = fract(depth * bitShift);
            rgbaDepth -= rgbaDepth.gbaa * bitMask;
            return rgbaDepth;
        }

        void main() {
            gl_FragColor = packDepth(gl_FragCoord.z);
        }
    </script>
    <script id="vertexShader" type="x-shader/x-vertex">
        //设置顶点着色器
        attribute vec4 a_Position;
        attribute vec4 a_Normal;
        attribute vec4 a_Color;
        uniform mat4 u_MvpMatrix;
        uniform mat4 u_MvpMatrixFromLight;
        varying vec4 v_PositionFromLight;
        varying vec4 v_Normal;
        varying vec4 v_Color;
        void main() {
            gl_Position = u_MvpMatrix * a_Position;
            v_PositionFromLight = u_MvpMatrixFromLight * a_Position;
            v_Color = a_Color;
            v_Normal = a_Normal;
        }

    </script>
    <script id="fragmentShader" type="x-shader/x-vertex">
        //设置片元着色器
        #ifdef GL_ES
        precision highp float;
        #endif
        uniform sampler2D u_Sampler;
        uniform vec3 u_DiffuseLight;
        uniform vec3 u_LightDirection;
        uniform vec3 u_AmbientLight;
        varying vec4 v_Color;
        varying vec4 v_Normal;
        varying vec4 v_PositionFromLight;
        float unpackDepth(const in vec4 rgbaDepth) {
            const vec4 bitShift = vec4(1.0, 1.0/256.0, 1.0/(256.0*256.0), 1.0/(256.0*256.0*256.0));
            float depth = dot(rgbaDepth, bitShift);
            return depth;

        }
        void main() {
            vec3 shadowCoord = (v_PositionFromLight.xyz/v_PositionFromLight.w)/2.0 + 0.5;
            vec4 rgbaDepth = texture2D(u_Sampler, shadowCoord.xy);
            float depth = unpackDepth(rgbaDepth);
            float visibility = (shadowCoord.z > depth + 0.005) ? 0.75 : 1.0;
            vec3 normal = normalize(v_Normal.xyz);
            float nDotL = max(dot(u_LightDirection, normal), 0.0);
            vec3 diffuse = u_DiffuseLight * v_Color.rgb * nDotL;
            vec3 ambient = u_AmbientLight * v_Color.rgb;
            gl_FragColor = vec4( (ambient +diffuse) * visibility, v_Color.a);
        }
    </script>
</head>
<body onload="main()">

<canvas id="canvas"></canvas>
<script src="libs/webgl_utils"></script>
<script src="libs/webgl_debug.js"></script>
<script src="libs/cuon_matrix.js"></script>
<script src="libs/cuon_utils.js"></script>
<script src="sunlightImitate.js"></script>
</body>
</html>
